; STANDARD HEADER FILE
	PROCESSOR 16F876A
; --- REGISTER FILES ---
; BANK 0
INDF	EQU	00H
TMR0	EQU	01H
PCL	EQU	02H
STATUS	EQU	03H
FSR	EQU	04H
PORTA	EQU	05H
PORTB	EQU	06H
PORTC	EQU	07H
PCLATH	EQU	0AH
INTCON	EQU	0BH
; BANK 1
OPTIONR	EQU	81H
TRISA	EQU	85H
TRISB	EQU	86H
TRISC	EQU	87H
ADCON1	EQU	9FH
; --- STATUS BITS ---
IRP	EQU	7
RP1	EQU	6
RP0	EQU	5
NOT_TO	EQU	4
NOT_PD	EQU	3
ZF	EQU	2	;ZERO FLAG BIT
DC	EQU	1	;DIGIT CARRY/BORROW BIT
CF	EQU	0	;CARRY/BORROW FLAG BIT
; --INTCON BITS ---
;
; --OPTION BITS ---

W	EQU	B'0'
F	EQU	.1

;==============================================USER DEFINE==========================================================

DISP1	EQU	21H;DG1
DISP2	EQU	22H;DG2
DISP3	EQU	23H;DG3
DISP4	EQU	24H;DG4
INT_CNT	EQU	25H;FOR TIME
D_1M	EQU	26H;1ST BIT OF MIN
D_10M	EQU	27H;2ND BIT OF MIN
D_1H	EQU	28H;1ST BIT OF HOUR
D_10H	EQU	29H;2ND BIT OF HOUR
C_SEC	EQU	30H;FOR SECOND COUNT
CNT	EQU	31H;FOR INTERRUPT EVENT
LOCK	EQU	32H;FOR LOCK
INT_CNT2	EQU	33H;FOR BUTTON INTERVAL

STATUS_TEMP	EQU	7EH
W_TEMP	EQU	7FH

;=============================================ORG 0000==============================================================

	ORG	0000
	GOTO	START_UP
	ORG	4
	
;*********************************************INTERRUPT*************************************************************
ISR
	MOVWF	W_TEMP
	SWAPF	STATUS,W
	MOVWF	STATUS_TEMP
	CALL	DISP 
	SWAPF	STATUS_TEMP,W
	MOVWF	STATUS
	SWAPF	W_TEMP,F
	SWAPF	W_TEMP,W
	BCF	INTCON,2
	RETFIE
	
;=============================================DISPLAY===============================================================
DISP	
	INCF	CNT,F;01(DG3) 10(DG2) 11(DG1) 00(DG4)
	BTFSC	CNT,1;1ST BIT TEST
	GOTO	DISP_HOUR
	GOTO	DISP_MIN
	
G_DP
	BTFSS	PORTC,4
	GOTO	DP_OFF
	GOTO	DP_ON
	
GGGG	BTFSS	PORTC,3	;segment g,segment dot on off
	GOTO	G_OFF
	GOTO	G_ON
	
DP_OFF
	BCF	PORTA,0
	BCF	PORTB,0
	GOTO	GGGG
DP_ON
	BSF	PORTA,0
	BSF	PORTB,0
	GOTO	GGGG

G_ON
	BSF	PORTA,1
	RETURN
G_OFF
	BCF	PORTA,1
	RETURN
	
	
DISP_MIN;0
	BTFSC	CNT,0
	GOTO	DISP_MIN_10
	GOTO	DISP_MIN_1	
DISP_MIN_1;00-4
	BSF	PORTA,3;DG1OFF
	MOVF	DISP4,W
	MOVWF	PORTC
	CALL	G_DP;G TEST
	BCF	PORTB,1;DG4ON
	INCF	INT_CNT
	RETURN
	
DISP_MIN_10;01-3
	BSF	PORTB,1;DG4OFF
	MOVF	DISP3,W
	MOVWF	PORTC
	CALL	G_DP;G TEST
	BCF	PORTB,2;DG3ON
	RETURN

DISP_HOUR;1
	BTFSC	CNT,0
	GOTO	DISP_HOUR_10
	GOTO	DISP_HOUR_1
DISP_HOUR_1;10-2
	BSF	PORTB,2;G3OFF
	MOVF	DISP2,W
	MOVWF	PORTC
	CALL	G_DP;G TEST
	CALL	DOTT
	BCF	PORTA,2;DG2ON
	INCF	INT_CNT
	RETURN
DISP_HOUR_10;11-1
	BSF	PORTA,2;DG2OFF
	MOVF	DISP1,W
	MOVWF	PORTC
	CALL	G_DP;G TEST
	BCF	PORTA,3;DG1ON
	RETURN
	
DOTT;0.5 초 간격 DOT 출력
	MOVLW	.0
	SUBWF	INT_CNT,W
	BTFSC	STATUS,0
	BSF	PORTA,0
	
	MOVLW	.122
	SUBWF	INT_CNT,W
	BTFSC	STATUS,0
	BCF	PORTA,0
	RETURN
	
;==============================================DISP_MODE==================================================

	
	
;**************************************************MAIN*******************************************************************
START_UP
	BSF	STATUS,RP0	;BANK1
	MOVLW	B'00000111'	;DIGITAL/ANALOG
	MOVWF	ADCON1
	MOVLW	B'00000000'
	MOVWF	TRISA
	MOVLW	B'01111000'
	MOVWF	TRISB
	MOVLW	B'00000000'
	MOVWF	TRISC
	MOVLW	B'00000010';weak_pullup/prescaler 8
	MOVWF	OPTIONR
	BCF	STATUS,RP0	;BANK0
	BSF	INTCON,5;TIMER INTERRUPT ENABLE
	BSF	INTCON,7;GIE
	
	
	CALL	INIT	;INITIALIZING ALL PORTS.
	
;==============================================CLOCK 24:00==========================================================
MAIN	
	MOVLW	.244
	SUBWF	INT_CNT,W
 	BTFSS	STATUS,ZF
 	GOTO	MODE
	
	CLRF	INT_CNT
	INCF	C_SEC
	MOVLW	.60
	SUBWF	C_SEC,W;60SEC -> 1MIN
	BTFSS	STATUS,ZF
	GOTO	MODE
	
	CLRF	C_SEC;00:0X
	INCF	D_1M
	MOVLW	.10
	SUBWF	D_1M,W
	BTFSS	STATUS,ZF
	GOTO	MODE
	
	CLRF	D_1M;00:X0
	INCF	D_10M
	MOVLW	.6
	SUBWF	D_10M,W
	BTFSS	STATUS,ZF
	GOTO	MODE

	CLRF	D_10M;0X:00
	INCF	D_1H
	MOVLW	.4
	SUBWF	D_1H,W
	BTFSS	STATUS,ZF
	GOTO	HOUR_COUNTINUE
	MOVLW	.2
	SUBWF	D_10H,W
	BTFSS	STATUS,ZF
	GOTO	HOUR_COUNTINUE
	GOTO	TIME_SET_ZERO
HOUR_COUNTINUE
	MOVLW	.10
	SUBWF	D_1H,W
	BTFSS	STATUS,ZF
	GOTO	MODE
	
	CLRF	D_1H;X0:00
	INCF	D_10H
	MOVLW	.2
	SUBWF	D_10H,W
	GOTO	MODE
	MOVLW	.4
	GOTO	MODE
	
TIME_SET_ZERO
	CLRF	D_1H
	CLRF	D_10H
	GOTO	MODE

;===================================================MODE============================================================
MODE	;LOCK 0:BUTTONLOCK	1:SW1
	BTFSS	LOCK,1;SW1
	GOTO	SW_I
	GOTO	MODE_1_LOCK;LOCK1ON -> MODE1
	
	
SW_I	BTFSS	PORTB,3;SW1
	GOTO	MODE_1
	BTFSS	PORTB,4;SW2
	GOTO	MODE_2
	BTFSS	PORTB,5;SW3
	GOTO	MODE_3
	CALL	DISP_MODE
	GOTO	MAIN
	
;========================================MODE1,TIMESET================================
MODE_1
	BSF	LOCK,1
	CLRF	INT_CNT
MODE_1_LOCK
	CALL	DISP_MODE1
	MOVLW	.50	;CHECK BUTTON PER 100
	SUBWF	INT_CNT,W
 	BTFSS	STATUS,ZF
 	GOTO	MODE_1_LOCK
	BTFSS	PORTB,4;SW2
	GOTO	INC_MIN
	BTFSS	PORTB,5;SW3
	GOTO	INC_HOUR
	GOTO	INT_CNT_TEST
	
	
INT_CNT_TEST
	MOVLW	.51	;CLR INT_CNT 101
	SUBWF	INT_CNT,W
 	BTFSS	STATUS,ZF
 	GOTO	MODE_1_LOCK
 	CLRF	INT_CNT
 	GOTO	MODE_1_LOCK
 	
INC_MIN;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLRF	INT_CNT
	INCF	D_1M
	MOVLW	.10
	SUBWF	D_1M,W
	BTFSS	STATUS,ZF
	GOTO	MODE_1_LOCK
	
	CLRF	D_1M
	INCF	D_10M
	MOVLW	.6
	SUBWF	D_10M,W
	BTFSS	STATUS,ZF
	GOTO	MODE_1_LOCK
	CLRF	D_10M
	GOTO	MODE_1_LOCK
INC_HOUR;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	CLRF	INT_CNT
	INCF	D_1H
	MOVLW	.4
	SUBWF	D_1H,W
	BTFSS	STATUS,ZF
	GOTO	HOUR_COUNTINUE2
	MOVLW	.2
	SUBWF	D_10H,W
	BTFSS	STATUS,ZF
	GOTO	HOUR_COUNTINUE2
	GOTO	TIME_SET_ZERO2
HOUR_COUNTINUE2
	MOVLW	.10
	SUBWF	D_1H,W
	BTFSS	STATUS,ZF
	GOTO	MODE_1_LOCK
	
	CLRF	D_1H
	INCF	D_10H
	GOTO	MODE_1_LOCK	
TIME_SET_ZERO2
	CLRF	D_1H
	CLRF	D_10H
	GOTO	MODE_1_LOCK
	
MODE_2
	NOP
	GOTO	MAIN

MODE_3
	NOP
	GOTO	MAIN
	
DISP_MODE1
DISP_MODE
	MOVF	D_1M,W
	CALL	CONV
	MOVWF	DISP4
	MOVF	D_10M,W
	CALL	CONV
	MOVWF	DISP3
	MOVF	D_1H,W
	CALL	CONV
	MOVWF	DISP2
	MOVF	D_10H,W
	CALL	CONV
	MOVWF	DISP1

	RETURN
;*************************************************SUBFUNCTION*******************************************************
INIT
	MOVLW	B'00011100'
	MOVWF	PORTA
	MOVLW	B'00111110'
	MOVWF	PORTB
	MOVLW	B'11100000'
	MOVWF	PORTC
	
	CLRF	CNT
	CLRF	DISP1
	CLRF	DISP2
	CLRF	DISP3
	CLRF	DISP4
	CLRF	D_1M
	CLRF	D_10M
	CLRF	D_1H
	CLRF	D_10H
	CLRF	C_SEC
	CLRF	LOCK
	
	RETURN

CONV
	ANDLW	0FH
	ADDWF	PCL,F
	
	RETLW	B'11100111';0
	RETLW	B'01100000';1
	RETLW	B'11001011';2
	RETLW	B'11101010';3
	RETLW	B'01101100';4
	RETLW	B'10101110';5
	RETLW	B'10101111';6
	RETLW	B'11100100';7
	RETLW	B'11101111';8
	RETLW	B'11101110';9
	RETLW	B'10001101';F
	RETLW	B'10001101';F
	RETLW	B'10001101';F
	
	END